/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright Decodable, Inc.
 *
 * Licensed under the Apache Software License version 2.0, available at http://www.apache.org/licenses/LICENSE-2.0
 */

plugins {
    id 'base'
    id 'eclipse'
    id 'com.diffplug.spotless'
    id 'maven-publish'
    id 'org.jreleaser'
}

allprojects {
    group='co.decodable'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'maven-publish'
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        withJavadocJar()
        withSourcesJar()
    }
    spotless {
        java {
            toggleOffOn()
            removeUnusedImports()
            googleJavaFormat('1.23.0')
            licenseHeader '''/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright Decodable, Inc.
 *
 * Licensed under the Apache Software License version 2.0, available at http://www.apache.org/licenses/LICENSE-2.0
 */'''
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = "${group}"
                artifactId = "${rootProject.name}"
                version = "${version}"
                
                from components.java

                pom {
                    name = 'Decodable Pipeline SDK'
                    description = 'A software development kit for implementing Apache Flink jobs and running them on Decodable'
                    url = 'https://github.com/decodableco/decodable-pipeline-sdk'
                    inceptionYear = '2023'
                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://spdx.org/licenses/Apache-2.0.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'gunnarmorling'
                            name = 'Gunnar Morling'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/decodableco/decodable-pipeline-sdk.git'
                        developerConnection = 'scm:git:ssh://github.com/decodableco/decodable-pipeline-sdk.git'
                        url = 'https://github.com/decodableco/decodable-pipeline-sdk'
                    }
                }

                pom.withXml {
                    asNode().dependencies.'*'.each() {
                        if (it.artifactId.text() == 'flink-core' || it.artifactId.text() == 'flink-streaming-java') {
                            it.scope*.value = 'provided'
                        }
                        else if (it.artifactId.text() == 'flink-connector-kafka') {
                            it.scope*.value = 'compile'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }

    tasks.named('jar') {
        manifest {
            attributes('Implementation-Title': project.name,
                    'Implementation-Version': project.version)
        }
    }

    tasks.named('test') {
        useJUnitPlatform() 
    }

    tasks.withType(Javadoc) {
        title "Decodable Pipeline SDK ${project.version}"
        exclude 'co/decodable/sdk/pipeline/internal'
        options.addStringOption('-snippet-path', sourceSets.test.java.srcDirs.first().getPath())
        options.header = 'Copyright ¬© 2023 - 2025 <a style="text-transform:none;" href="https://www.decodable.co">Decodable, Inc.</a>; Licensed under the Apache License, version 2.0.'
        javadocTool = javaToolchains.javadocToolFor {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}

// prevent root project from building a jar
tasks.register('jar') {
    enabled = false
}

jreleaser {
    gitRootSearch = true
    project {
        versionPattern = 'CUSTOM'
        version='1.0.0.Alpha3'
        java {
            groupId = "${group}"
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            nexus2 {
                'maven-central' {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
                    closeRepository = true
                    releaseRepository = true
                    sign = true
                    //NOTE: staging repositories added dynamically see below
                }
            }
        }
    }

    // adding all staging-deploy directories from subprojects
    subprojects.each { p ->
        jreleaser.deploy.maven.nexus2.'maven-central'.stagingRepository("${p.buildDir}/staging-deploy")
    }

    release {
        github {
            issues {
                enabled = true
                applyMilestone = 'ALWAYS'
            }
            changelog {
                formatted = 'ALWAYS'
                format = '- {{commitShortHash}} {{commitTitle}}'
                labeler {
                    label = 'feature'
                    title = 'regex:.*#[0-9] feat: .*'
                }
                labeler {
                    label = 'issue'
                    title = 'regex:.*#[0-9] fix: .*'
                }
                labeler {
                    label = 'task'
                    title = 'regex:.*#[0-9] chore: .*'
                }
                labeler {
                    label = 'task'
                    title = 'regex:.*Bump.*'
                }
                category {
                    title = 'üöÄ Features'
                    key = 'features'
                    labels = ['feature']
                }
                category {
                    title = '‚úÖ Issues'
                    key = 'issues'
                    labels = ['issue']
                }
                category {
                    title = 'üß∞ Tasks'
                    key = 'tasks'
                    labels = ['task']
                }
                category {
                    title = '‚öôÔ∏è  Ô∏èDependencies'
                    key = 'dependencies'
                    labels = ['dependency']
                }
                hide {
                    contributors = ['GitHub', 'decodable-release-bot']
                }
            }
        }
    }
}
